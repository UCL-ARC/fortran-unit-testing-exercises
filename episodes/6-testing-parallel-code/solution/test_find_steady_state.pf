!> Module for testing the subroutine game_of_life::find_steady_state
module test_find_steady_state
    use game_of_life, only : find_steady_state
    ! allow(C121)
    use pfunit
    implicit none

    public

    !> Type to bundle inputs and expected outputs of game_of_life::find_steady_state
    @testParameter
    type, extends(MPITestParameter) :: find_steady_state_test_params
        !> The initial starting board to be passed into find_steady_state
        integer, dimension(:,:), allocatable :: input_board
        !> The expected steady state result
        logical :: expected_steady_state
        !> The expected number of generations to reach steady state
        integer :: expected_generation_number
        !> A description of the test to be outputted for logging
        character(len=100) :: description
    contains
        procedure :: toString => find_steady_state_test_params_toString
    end type find_steady_state_test_params

    !> Type to define a single find_steady_state test case
    @TestCase(testParameters={getTestSuite()}, constructor=paramsToCase)
    type, extends(MPITestCase) :: find_steady_state_test_case
        type(find_steady_state_test_params) :: params
    end type find_steady_state_test_case

contains

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! Test Suites
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    !> Test suite for the game_of_life::find_steady_state subroutine
    function getTestSuite() result(params)
        !> The array of test parameters
        type(find_steady_state_test_params), allocatable :: params(:)

        integer :: i, max_num_ranks
        integer, dimension(:,:), allocatable :: board

        !  Steady state should be reached after 17 iterations
        !       8  9 10 11 12
        !      -- -- -- -- --
        !   8 | 0  0  0  0  0
        !   9 | 0  0  1  0  0
        !  10 | 0  1  1  1  0
        !  11 | 0  1  0  1  0
        !  12 | 0  0  1  0  0
        !  13 | 0  0  0  0  0
        ! global_board only needs to be set for rank 0 as this should be distributed to other ranks in the src code
        allocate(board(31, 31))
        board = 0
        board(9,9:11)  = [0,1,0]
        board(10,9:11) = [1,1,1]
        board(11,9:11) = [1,0,1]
        board(12,9:11) = [0,1,0]

        max_num_ranks = 8
        allocate(params(max_num_ranks))
        do i = 1, max_num_ranks
            params(i) = find_steady_state_test_params(i, board, .true., 17, "an exploder initial state")
        end do
    end function getTestSuite

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! Assertion functions
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    !> Check for the expected output of the game_of_life::find_steady_state subroutine
    @Test
    subroutine TestFindSteadyState(this)
        !> The current test case including inputs and expected outputs
        class(find_steady_state_test_case), intent(inout) :: this

        logical :: actual_steady_state
        integer :: actual_generation_number

        call find_steady_state(actual_steady_state, actual_generation_number, this%params%input_board, &
            size(this%params%input_board, 1), size(this%params%input_board, 2), this%getMpiCommunicator(), &
            this%getNumProcessesRequested())

        @assertEqual(this%params%expected_generation_number, actual_generation_number, "Unexpected generation_number")

        @assertTrue(this%params%expected_steady_state .eqv. actual_steady_state, "Unexpected steady_state value")

    end subroutine TestFindSteadyState

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! Constructors
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    !> Constructor for find_steady_state_test_case
    function paramsToCase(testParameter) result(tst)
        !> The test parameters for the new test case
        type(find_steady_state_test_params), intent(in) :: testParameter
        !> The new test case
        type(find_steady_state_test_case) :: tst

        tst%params = testParameter
    end function paramsToCase

    !> Converts find_steady_state_test_params to a string for logging
    function find_steady_state_test_params_toString(this) result(string)
        !> The test parameters to convert
        class (find_steady_state_test_params), intent(in) :: this
        !> The resulting string representation
        character(:), allocatable :: string

        character(len=80) :: buffer

        integer :: nrow, ncol

        nrow = size(this%input_board, 1)
        ncol = size(this%input_board, 2)

        write(buffer,'(i2, "x", i2, " board with ", a)') &
            nrow, ncol, trim(this%description)
        string = trim(buffer)
    end function find_steady_state_test_params_toString

end module test_find_steady_state
