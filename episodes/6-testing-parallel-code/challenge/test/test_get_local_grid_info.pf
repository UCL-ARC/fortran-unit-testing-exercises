!> Module for testing the subroutine game_of_life::get_local_grid_info
module test_get_local_grid_info
    use comms, only : get_local_grid_info, DomainDecomposition, DOWN, LEFT, UP, RIGHT
    ! allow(C121)
    use pfunit
    implicit none

    public

    !> Type to bundle inputs and expected outputs of game_of_life::get_local_grid_info
    @testParameter
    type, extends(MPITestParameter) :: get_local_grid_info_test_params
        integer, allocatable :: ncols_per_rank(:), nrows_per_rank(:), coords(:,:), neighbours(:,:),col_start(:),row_start(:), &
                                local_ncols(:), local_nrows(:)
    end type get_local_grid_info_test_params

    !> Type to define a single get_local_grid_info test case
    @TestCase(testParameters={getParameters()}, constructor=newTest)
    type, extends(MPITestCase) :: get_local_grid_info_test_case
        type(get_local_grid_info_test_params) :: params
        integer :: global_ncols, global_nrows
        type(DomainDecomposition) :: domainDecomp
    contains
        procedure :: setUp
    end type get_local_grid_info_test_case

contains

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! State setup (Given)
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    !> Setup the variables shared by each rank - global_ncols, the cartesian communicator and rank dimensions.
    subroutine setUp(this)
        class(get_local_grid_info_test_case), intent(inout) :: this

        integer :: ierr

        this%global_ncols = 31
        this%global_nrows = 31

        this%domainDecomp%dims = 0
        call MPI_Dims_create(this%getNumProcesses(), 2, this%domainDecomp%dims, ierr)
        call MPI_Cart_create( &
            this%getMpiCommunicator(), 2, this%domainDecomp%dims, [.false.,.false.], .true., this%domainDecomp%communicator, ierr)
    end subroutine setUp

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! Test Suites
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    !> Test suite for the game_of_life::get_local_grid_info subroutine
    function getParameters() result(params)
        type(get_local_grid_info_test_params), allocatable :: params(:)

        integer :: max_num_ranks, num_ranks, rank, i
        integer, allocatable :: ncols_per_rank(:), nrows_per_rank(:), coords(:,:), neighbours(:,:),col_start(:),row_start(:), &
                                local_ncols(:), local_nrows(:)

        max_num_ranks = 4
        allocate(params(max_num_ranks))

        allocate(ncols_per_rank(max_num_ranks))
        allocate(nrows_per_rank(max_num_ranks))
        allocate(coords(2,max_num_ranks))
        allocate(neighbours(4,max_num_ranks))
        allocate(col_start(max_num_ranks))
        allocate(row_start(max_num_ranks))
        allocate(local_ncols(max_num_ranks))
        allocate(local_nrows(max_num_ranks))

        do num_ranks = 1, max_num_ranks
            ! Initialise neighbours to catch any mistakes in parameter setup
            neighbours = -2

            do i = 1, num_ranks
                rank = i-1

                ! Define expected outputs for 1 rank
                if (num_ranks == 1) then
                    ncols_per_rank(i) = 31
                    nrows_per_rank(i) = 31
                    coords(:,i) = [0,0]
                   col_start(i) = 1
                   row_start(i) = 1
                    local_ncols(i) = 31
                    local_nrows(i) = 31

                ! Define expected outputs for 2 ranks
                else if (num_ranks == 2) then
                    ncols_per_rank(i) = 15
                    nrows_per_rank(i) = 31
                   row_start(i) = 1
                    local_nrows(i) = 31
                    if (rank == 0) then
                        coords(:,i) = [0,0]
                        neighbours(UP,i) = 1
                       col_start(i) = 1
                        local_ncols(i) = 15
                    else if (rank == 1) then
                        coords(:,i) = [1,0]
                        neighbours(DOWN,i) = 0
                       col_start(i) = 16
                        local_ncols(i) = 16
                    end if

                ! Define expected outputs for 3 ranks
                else if (num_ranks == 3) then
                    ncols_per_rank(:) = 10
                    nrows_per_rank(:) = 31
                   row_start(:) = 1
                    local_nrows(:) = 31
                    if (rank == 0) then
                        coords(:,i) = [0,0]
                        neighbours(UP,i) = 1
                       col_start(i) = 1
                        local_ncols(i) = 10
                    else if (rank == 1) then
                        coords(:,i) = [1,0]
                        neighbours(DOWN,i) = 0
                        neighbours(UP,i) = 2
                       col_start(i) = 11
                        local_ncols(i) = 10
                    else if (rank == 2) then
                        coords(:,i) = [2,0]
                        neighbours(DOWN,i) = 1
                       col_start(i) = 21
                        local_ncols(i) = 11
                    end if

                ! Define expected outputs for 4 ranks
                else if (num_ranks == 4) then
                    ncols_per_rank(i) = 15
                    nrows_per_rank(i) = 15
                    if (rank == 0) then
                        coords(:,i) = [0,0]
                        neighbours(UP,i) = 2
                        neighbours(RIGHT,i) = 1
                       col_start(i) = 1
                       row_start(i) = 1
                        local_ncols(i) = 15
                        local_nrows(i) = 15
                    else if (rank == 1) then
                        coords(:,i) = [0,1]
                        neighbours(LEFT,i) = 0
                        neighbours(UP,i) = 3
                       col_start(i) = 1
                       row_start(i) = 16
                        local_ncols(i) = 15
                        local_nrows(i) = 16
                    else if (rank == 2) then
                        coords(:,i) = [1,0]
                        neighbours(DOWN,i) = 0
                        neighbours(RIGHT,i) = 3
                       col_start(i) = 16
                       row_start(i) = 1
                        local_ncols(i) = 16
                        local_nrows(i) = 15
                    else if (rank == 3) then
                        coords(:,i) = [1,1]
                        neighbours(DOWN,i) = 1
                        neighbours(LEFT,i) = 2
                       col_start(i) = 16
                       row_start(i) = 16
                        local_ncols(i) = 16
                        local_nrows(i) = 16
                    end if

                end if
            end do
            params(num_ranks) = get_local_grid_info_test_params( &
                num_ranks, ncols_per_rank, nrows_per_rank, coords, neighbours,col_start,row_start, local_ncols, local_nrows &
            )
        end do
    end function getParameters

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! Assertion functions
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    !> Check for the expected output of the game_of_life::get_local_grid_info subroutine
    @Test
    subroutine TestGetLocalGridInfo(this)
        class(get_local_grid_info_test_case), intent(inout) :: this

        integer :: ncols_per_rank, nrows_per_rank, coords(2),col_start,row_start, local_ncols, local_nrows, ierr

        integer :: rank, rank_index

        rank = this%getProcessRank()
        rank_index = rank + 1

        call get_local_grid_info(this%domainDecomp, rank, this%global_nrows, this%global_ncols, &
            nrows_per_rank, ncols_per_rank, coords,row_start,col_start, local_nrows, local_ncols)

        @assertEqual(this%params%ncols_per_rank(rank_index), ncols_per_rank, "Unexpected value for ncols_per_rank")
        @assertEqual(this%params%nrows_per_rank(rank_index), nrows_per_rank, "Unexpected value for nrows_per_rank")
        @assertEqual(this%params%coords(:,rank_index), coords, "Unexpected value for coords")
        @assertEqual(this%params%neighbours(:,rank_index), this%domainDecomp%neighbours, "Unexpected value for neighbours")
        @assertEqual(this%params%col_start(rank_index),col_start, "Unexpected value forcol_start")
        @assertEqual(this%params%row_start(rank_index),row_start, "Unexpected value forrow_start")
        @assertEqual(this%params%local_ncols(rank_index), local_ncols, "Unexpected value for local_ncols")
        @assertEqual(this%params%local_nrows(rank_index), local_nrows, "Unexpected value for local_nrows")
    end subroutine TestGetLocalGridInfo

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! Constructors
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    function newTest(testParameter) result(tst)
        type(get_local_grid_info_test_case) :: tst
        type(get_local_grid_info_test_params), intent(in) :: testParameter

        tst%params = testParameter
    end function newTest
end module test_get_local_grid_info
