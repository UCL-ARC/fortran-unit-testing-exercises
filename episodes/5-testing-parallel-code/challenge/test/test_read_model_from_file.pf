module test_read_model_from_file
    use game_of_life_mod, only : read_model_from_file
    ! allow(C121)
    use funit

    implicit none

    public

    !> Type to bundle inputs and expected outputs of game_of_life::read_model_from_file
    @testParameter
    type, extends(AbstractTestParameter) :: read_model_from_file_test_params
        character(len=:), allocatable :: input_fname
        integer :: max_nrow
        integer :: max_ncol
        integer, dimension(:,:), allocatable :: expected_board
        character(len=:), allocatable :: expected_io_error_message
        logical :: model_is_valid
    contains
        procedure :: toString => read_model_from_file_test_params_toString
    end type read_model_from_file_test_params

    !> Type to define a single evolve_board test case
    @TestCase(testParameters={getParameters()}, constructor=newTest)
    type, extends(ParameterizedTestCase) :: read_model_from_file_test_case
        type(read_model_from_file_test_params) :: params
        logical :: model_is_valid
    end type read_model_from_file_test_case

contains

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! Test Suites
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    !> Test suite for the game_of_life::read_model_from_file subroutine
    function getParameters() result(params)
        type (read_model_from_file_test_params), allocatable :: params(:)

        integer, dimension(:,:), allocatable :: test_board
        character(len=:), allocatable :: test_io_error_message

        allocate(params(6))

        allocate(test_board(31, 31))
        test_board = 0

        allocate(character(len=80) :: test_io_error_message)
        params(1) = read_model_from_file_test_params( &
            "test-models/zeros_31_31.dat", 100, 100, test_board, test_io_error_message, .true. &
        )

        test_io_error_message = "nx must be a positive integer less than     10 found     31"
        params(2) = read_model_from_file_test_params( &
            "test-models/zeros_31_31.dat", 10, 100, test_board, test_io_error_message, .false. &
        )

        test_io_error_message = "ny must be a positive integer less than     10 found     31"
        params(3) = read_model_from_file_test_params( &
            "test-models/zeros_31_31.dat", 100, 10, test_board, test_io_error_message, .false. &
        )

        test_io_error_message = "nx must be a positive integer less than    100 found    -10"
        params(4) = read_model_from_file_test_params( &
            "test-models/empty_-10_10.dat", 100, 100, test_board, test_io_error_message, .false. &
        )

        test_io_error_message = "ny must be a positive integer less than    100 found    -10"
        params(5) = read_model_from_file_test_params( &
            "test-models/empty_10_-10.dat", 100, 100, test_board, test_io_error_message, .false. &
        )

        test_io_error_message = " *** Error when opening does/not/exist.dat"
        params(6) = read_model_from_file_test_params( &
            "does/not/exist.dat", 100, 100, test_board, test_io_error_message, .false. &
        )
    end function getParameters

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! Assertion functions
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    @Test
    subroutine TestCheckReadModelFromFile(this)
        class (read_model_from_file_test_case), intent(inout) :: this

        integer, dimension(:,:), allocatable :: actual_board
        character(len=:), allocatable :: actual_io_error_message

        call read_model_from_file(this%params%input_fname, this%params%max_nrow, this%params%max_ncol, actual_board, &
                                  actual_io_error_message)

        if (this%params%model_is_valid) then
            call VerifyCheckReadModelFromFileValidModel(this%params, actual_board, actual_io_error_message)
        else
            call VerifyCheckReadModelFromFileInvalidModel(this%params, actual_board, actual_io_error_message)
        end if
    end subroutine TestCheckReadModelFromFile

    subroutine VerifyCheckReadModelFromFileValidModel(params, actual_board, actual_io_error_message)
        class (read_model_from_file_test_params), intent(inout) :: params

        integer, dimension(:,:), allocatable, intent(in) :: actual_board
        character(len=:), allocatable, intent(in) :: actual_io_error_message

        @assertFalse(allocated(actual_io_error_message), "Expected actual_io_error_message to not be allocated")
        @assertTrue(allocated(actual_board), "Expected actual_board to be allocated")
        @assertEqual(params%expected_board, actual_board, "Actual and expected boards do not match")
    end subroutine VerifyCheckReadModelFromFileValidModel

    !> Check for the expected output of the game_of_life::read_model_from_file subroutine
    subroutine VerifyCheckReadModelFromFileInvalidModel(params, actual_board, actual_io_error_message)
        class (read_model_from_file_test_params), intent(inout) :: params

        integer, dimension(:,:), allocatable, intent(in) :: actual_board
        character(len=:), allocatable, intent(in) :: actual_io_error_message

        @assertTrue(allocated(actual_io_error_message), "Expected actual_io_error_message to be allocated")
        @assertEqual(params%expected_io_error_message, actual_io_error_message, "Actual and expected error messages do not match")
        @assertFalse(allocated(actual_board), "Expected actual_board to not be allocated")
    end subroutine VerifyCheckReadModelFromFileInvalidModel

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! Contructors
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    function newTest(testParameter) result(tst)
        type (read_model_from_file_test_case) :: tst
        type (read_model_from_file_test_params), intent(in) :: testParameter

        integer :: nx, ny

        nx = size(testParameter%expected_board, 1)
        ny = size(testParameter%expected_board, 2)
        allocate(tst%params%expected_board(nx, ny))

        tst%params%input_fname = testParameter%input_fname
        tst%params%max_nrow = testParameter%max_nrow
        tst%params%max_ncol = testParameter%max_ncol
        tst%params%expected_board = testParameter%expected_board
        tst%params%expected_io_error_message = testParameter%expected_io_error_message
        tst%params%model_is_valid = testParameter%model_is_valid
    end function newTest

    function read_model_from_file_test_params_toString(this) result(string)
        class (read_model_from_file_test_params), intent(in) :: this
        character(:), allocatable :: string

        character(len=80) :: buffer

        if (this%model_is_valid) then
            write(buffer,'("Valid model",a,i3,i3)') &
                this%input_fname, this%max_nrow, this%max_ncol
        else
            write(buffer,'("Invalid model",a,i3,i3)') &
                this%input_fname, this%max_nrow, this%max_ncol
        end if
        string = trim(buffer)

    end function read_model_from_file_test_params_toString
end module test_read_model_from_file
